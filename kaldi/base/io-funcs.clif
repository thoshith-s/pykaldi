from "base/iostream-clifwrap.h" import *

from kaldi.util import _value_error_on_false

from "base/io-funcs.h":
  namespace `kaldi`:

    def `WriteToken` as write_token(os: ostream, binary: bool, token: str):
      """Writes non-empty string of non-space characters."""

    def `Peek` as peek(is: istream, binary: bool) -> int:
      """Consumes whitespace (if binary == True) and returns the peek value."""

    def `ReadToken` as read_token(is: istream, binary: bool) -> str:
      """Reads and returns the next token (throws exception on failure)"""

    def `PeekToken` as peek_token(is: istream, binary: bool) -> int:
      """Returns the first character of the next token or -1 if EOF."""

    def `ExpectToken` as expect_token(is: istream, binary: bool, token: str):
      """Tries to read the given token and throws exception on failure."""

    def `WriteIntegerVector` as write_int_vector(os: ostream, binary: bool, v: list<int>):
      """Writes a sequence of integers."""

    def `ReadIntegerVector` as read_int_vector(is: istream, binary: bool) -> list<int>:
      """Reads a sequence of integers."""

    def `WriteIntegerPairVector` as write_int_pair_vector(os: ostream, binary: bool, v: list<tuple<int, int>>):
      """Writes a sequence of integer pairs."""

    def `ReadIntegerPairVector` as read_int_pair_vector(is: istream, binary: bool) -> list<tuple<int, int>>:
      """Reads a sequence of integer pairs."""

    def `InitKaldiOutputStream` as init_kaldi_output_stream(os: ostream, binary: bool):
      """Initializes an output stream by writing an optional binary header."""

    def `InitKaldiInputStream` as init_kaldi_input_stream(is: istream) -> (success: bool, binary: bool):
      """Initializes an input stream by detecting the binary header."""
      return _value_error_on_false(...)
